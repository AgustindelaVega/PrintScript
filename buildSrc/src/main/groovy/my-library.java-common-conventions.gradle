plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.sherter.google-java-format'
    id 'checkstyle'
    id("maven-publish")
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13'
    implementation 'com.google.code.gson:gson:2.8.6'
}

test {
    finalizedBy jacocoTestReport
}

check.dependsOn jacocoTestCoverageVerification

jacoco {
    toolVersion = "0.8.6"
}

checkstyle {
    toolVersion "8.41.1"
    configFile file("${rootDir}/config/checkstyle/checkstyle.xml")
}

checkstyleMain {
    source = 'src/main/java'
}
checkstyleTest {
    source = 'src/test/java'
}

check.dependsOn(checkstyleMain)
check.dependsOn(checkstyleTest)

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                minimum = 0.80
            }
        }
    }
}

task format {
    dependsOn tasks.googleJavaFormat
}

task copyGitHooks(type: Copy) {
    from "$rootDir/config/git-hooks"
    into "$rootDir/.git/hooks"
}

build.dependsOn copyGitHooks

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/AgustindelaVega/PrintScript")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
