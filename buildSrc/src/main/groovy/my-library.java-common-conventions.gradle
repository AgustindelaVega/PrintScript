plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.sherter.google-java-format'
    id 'checkstyle'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'junit:junit:4.13'
}

test {
    finalizedBy jacocoTestReport
}

check.dependsOn jacocoTestCoverageVerification

jacoco {
    toolVersion = "0.8.6"
}

checkstyle {
    toolVersion "8.41.1"
    configFile file("${rootDir}/config/checkstyle/checkstyle.xml")
}

checkstyleMain {
    source = 'src/main/java'
}
checkstyleTest {
    source = 'src/test/java'
}


jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'LINE'
                minimum = 0.75
            }
        }
    }
}

task format {
    dependsOn tasks.googleJavaFormat
}

task copyGitHooks(type: Copy) {
    from "$rootDir/config/git-hooks"
    into "$rootDir/.git/hooks"
}

build.dependsOn copyGitHooks